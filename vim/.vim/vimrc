"===[ Plugin ]=== {{{
filetype off
set nocompatible
set rtp+=~/.vim/bundle/Vundle.vim
call vundle#begin()

Plugin 'VundleVim/Vundle.vim'

Plugin 'scrooloose/nerdtree'
" NERDTree {{{
augroup usr_nerdtree
    autocmd!
    autocmd StdinReadPre * let s:std_in=1
    autocmd BufEnter * if (winnr("$") == 1 && exists("b:NERDTree") && b:NERDTree.isTabTree()) | q | endif
augroup END

let NERDTreeIgnore=['\.log$', '\.git', '\.egg-info', '__pycache__', '\.pyc$', '.exe$', '\.ipynb_checkpoints', '\.pytest_cache', 'log$', '\.vscode']
let NERDTreeStatusLine="%{matchstr(getline('.'),'\\s\\zs\\w\\(.*\\)')}"
let NERDTreeShowHidden=1

" }}}
Plugin 'airblade/vim-gitgutter'
Plugin 'tpope/vim-fugitive'
Plugin 'henrik/vim-indexed-search'
"Plugin 'ap/vim-buftabline'
" BufTabLine {{{
"hi default link BufTabLineCurrent TabLineSel
"hi default link BufTabLineActive TabLine
"hi default link BufTabLineHidden TabLine
"hi default link BufTabLineFill TabLineFill
"
"let g:buftabline_numbers = 1
" }}}
Plugin 'godlygeek/tabular'
Plugin 'othree/html5.vim'
" HTML5 {{{
"let g:html_exclude_tags = ['html', 'body', 'head']
" }}}
Plugin 'leafgarland/typescript-vim'
Plugin 'mustache/vim-mustache-handlebars'
Plugin 'cespare/vim-toml'
Plugin 'Valloric/YouCompleteMe'
" YouCompleteMe {{{
let g:ycm_min_num_of_chars_for_completion = 2
let g:ycm_max_num_candidates = 25
let g:ycm_filetype_blacklist = {
    \ 'tagbar': 1,
    \ 'qf': 1,
    \ 'notes': 1,
    \ 'markdown': 1,
    \ 'unite': 1,
    \ 'text': 1,
    \ 'vimwiki': 1,
    \ 'pandoc': 1,
    \ 'infolog': 1,
    \ 'mail': 1,
    \ 'html': 1,
    \ 'gitconfig': 1,
    \ 'tex': 1,
    \ 'bib': 0,
    \}
let g:ycm_use_ultisnips_completer = 1

" YCM diagnostic
hi YcmErrorSection cterm=underline ctermfg=196
hi YcmWarningSection cterm=underline ctermfg=196
let g:ycm_error_symbol = 'x'
let g:ycm_warning_symbol = '>'
let g:ycm_open_loclist_on_ycm_diags = 0
let g:ycm_enable_diagnostic_highlighting = 0
let g:ycm_autoclose_preview_window_after_completion = 1

" YCM paths
let g:ycm_global_ycm_extra_conf = "/home/alan/.vim/.ycm_extra_conf.py"
let g:ycm_confirm_extra_conf = 0
let g:ycm_filepath_completion_use_working_dir = 1
let g:ycm_extra_conf_vim_data = ['&filetype']
let g:ycm_python_binary_path = '/usr/bin/python3'
" }}}
Plugin 'SirVer/ultisnips'
Plugin 'honza/vim-snippets'
Plugin 'zirrostig/vim-schlepp'
" Schlepp {{{
let g:Schlepp#allowSquishingLines = 0
let g:Schlepp#allowSquishingBlock = 0
let g:Schlepp#trimWS = 0
" }}}
Plugin 'jlanzarotta/bufexplorer'
Plugin 'pangloss/vim-javascript'


call vundle#end()
filetype plugin indent on
syntax on


" }}}
"===[ Color scheme ]=== {{{
colorscheme onedark
set background=dark


" }}}
"===[ Default Syntax Package Options ]=== {{{
let g:tex_no_error = 1


" }}}
"===[ Options ]=== {{{
set backspace=eol,start,indent
set cursorline
set nofoldenable
set hidden
set history=500
set hlsearch
set incsearch
set lazyredraw
set mouse=a
set noshowmatch
set number
set relativenumber
set scrolloff=7
set whichwrap+=<,>,h,l

set laststatus=2
set statusline=%#StatusLineGit#%{GitBranch()}%#User{1}#\ %{HasPaste()}%r%m%t%=%y\ %l,\%-2c\ \|%3p%%

set showtabline=1
set tabline=%!MyTabLine()

set splitright

" Tab, indent, wrap
set smarttab
set shiftwidth=4
set softtabstop=4
set tabstop=8

set autoindent
set smartindent
set expandtab
set list
exec "set listchars=tab:\u2015\u2015,trail:\uB7"

set colorcolumn=81
set formatoptions+=t
set textwidth=100
set nowrap

" Regex
set ignorecase
set magic
set smartcase

" Files, backups and undo
set fileformat=unix
set fileencoding=utf8
set encoding=utf8
set noswapfile
set nowritebackup

" Command-line completion
set path=.,,**
set wildmenu
set wildignore=__pycache__,*.o,*.pyc,*.git,*.exe
set helpheight=30

" Make
set shellpipe=&>


" }}}
"===[ Mapping ]=== {{{
let mapleader=" "

" Thank me later
noremap ; :
noremap : ;

" Homerow navigation
noremap H 0
noremap J L
noremap K H
noremap L $

noremap $ J
noremap 0 K

" Swap undo
noremap    U   <c-r>
noremap <c-r>     U

" Windows navigation
noremap [p          <c-w>P
noremap [h          <c-w>h
noremap [j          <c-w>j
noremap [k          <c-w>k
noremap [l          <c-w>l
noremap [o          <c-w>o
noremap [<s-h>      <c-w>9<
noremap [<s-j>      <c-w>9-
noremap [<s-k>      <c-w>9+
noremap [<s-l>      <c-w>9>

" Buffer/Error list shortcuts
noremap <c-s>      :update<cr>
noremap <c-q>      :qa!<cr>
noremap <c-h>      :tabprev<cr>
noremap <c-l>      :tabnext<cr>
noremap <c-j>      :bnext<cr>
noremap <c-k>      :bprev<cr>
noremap <c-n>      :cnext<cr>
noremap <c-p>      :cprev<cr>

" Normal mode command shortchuts
nnoremap <cr>               o<esc>
nnoremap <F9>               :echo HighlightName()<cr>
nnoremap <leader>c          :echo HighlightName()<cr>
nnoremap <leader>w          :w !sudo tee % > /dev/null
nnoremap <leader>f          :set filetype=

nnoremap <silent> -             :call Underline('-')<cr>
nnoremap <silent> =             :call Underline('=')<cr>
nnoremap <silent> <leader>'     :vsp<cr>
nnoremap <silent> <leader>e     :vsplit /home/alan/.vim/vimrc<cr>
nnoremap <silent> <leader>m     :silent! make \| redraw! \| call ReportBuildResult()<cr>
nnoremap <silent> <leader>n     :vnew \| set ft=markdown<cr>
nnoremap <silent> <leader>p     :setlocal paste!<cr>
nnoremap <silent> <leader>q     :q<cr>

" toggles
nnoremap <leader>s          :setlocal spell!<cr>
nnoremap <leader><tab>      :setlocal wrap!<cr>
nnoremap <leader><leader>   :setlocal hlsearch!<cr>
nnoremap <leader><esc>      :call ToggleFastEsc()<cr>

" Plugin mapping
nnoremap  <c-e> :NERDTreeToggle<cr>

nnoremap <leader>t      :Tabularize /

let g:UltiSnipsExpandTrigger="<c-e>"
let g:UltiSnipsJumpForwardTrigger="<c-j>"
let g:UltiSnipsJumpBackwardTrigger="<c-k>"

vmap <unique> <up>      <Plug>SchleppUp
vmap <unique> <down>    <Plug>SchleppDown
vmap <unique> <left>    <Plug>SchleppLeft
vmap <unique> <right>   <Plug>SchleppRight


" }}}
"===[ Abbrevation ]=== {{{
" Common typos
iabbrev fro         for
iabbrev adn         and
iabbrev swithc      switch
iabbrev swihtc      switch
iabbrev siwthc      switch
iabbrev csae        case
iabbrev caes        case


" }}}
"===[ Helper functions]=== {{{
function! HasPaste()
    if &paste
        return 'PASTE MODE'
    endif
    return ''
endfunction

" Format home directory into ~ and truncate the beginning of the path.
function! ShortCwd(len)
    let cwd=getcwd()
    let cwd=substitute(cwd, '/home/[^/]\+', '~', '')
    let cwd=substitute(cwd, '[^~].*\(.\{' . a:len . '\}$\)\@=', '/...', '')
    let cwd=substitute(cwd, '\.\.\.[^/]*', '...', '')
    return cwd
endfunction

function! PathFilename(path)
    let name=split(a:path, '/')
    if len(name) == 0
        return '[no name]'
    else
        return name[-1]
endfunction

function! GitBranch()
    let l:branchname = system("git rev-parse --abbrev-ref HEAD 2>/dev/null | tr -d '\n'")
    return strlen(l:branchname) > 0 ? '  ' . l:branchname . ' ' : ''
endfunction

function! MyTabLine()
    let s = ''
    for i in range(tabpagenr('$'))
        if i + 1 == tabpagenr()
            let s .= '%#TabLineSel#'
        else
            let s .= '%#TabLine#'
        endif
        let s .= '%' . (i + 1) . 'T'
        let s .= ' %{MyTabLabel(' . (i + 1) . ')} '
    endfor

    let s .= '%#TabLineFill#%T'

    if tabpagenr('$') > 1
        let s .= '%=%#TabLine#%999Xclose'
    endif
    return s
endfunction

function! MyTabLabel(n)
    let winnr = tabpagewinnr(a:n, '$')
    let bufls = tabpagebuflist(a:n)
    return a:n . ':' . '(' . winnr . ') ' . PathFilename(bufname(bufls[winnr - 1]))
endfunction

function! HighlightName()
    let id  = synID(line("."), col("."), 1)
    let tid = synID(line("."), col("."), 0)

    let fg  = synIDattr(synIDtrans(id), "fg")
    let bg  = synIDattr(synIDtrans(id), "bg")
    let hi  = synIDattr(id, "name")
    let lo  = synIDattr(synIDtrans(id), "name")
    let tr  = synIDattr(tid, "name")

    return "hi: ".hi." \| lo: ".lo." \| trans: ".tr." \| fg: ".fg." \| bg: ".bg
endfunction

function! EnableFastEsc()
    let b:fastesc=1
    inoremap <buffer> jj <esc>
    echom "Enabled insert mdoe fast <esc>"
endfunction!

function! DisableFastEsc()
    unlet b:fastesc
    iunmap <buffer> jj
    echom "Disabled insert mdoe fast <esc>"
endfunction!

function! ToggleFastEsc()
    if get(b:, 'fastesc', 0)
        call DisableFastEsc()
    else
        call EnableFastEsc()
    endif
endfunction

function! Underline(symbol)
    let line = getline('.')
    let llen = strlen(line)
    let str = ''
    if llen > 0
        let widx = match(line, '\S')
        for ii in range(widx)
            let str = str . " "
        endfor
        for ii in range(llen - widx)
            let str = str . a:symbol
        endfor
        call setreg("y", str)
        exec "normal! o\<esc>\"yp"
    endif
endfunction

function! ReportBuildResult()
    if len(getqflist()) != 0
        copen | cc
        echohl Tag
        echo "Build fail!"
        echohl None
    else
        echo "Build success!"
    endif
endfunction

function! TrimSpace()
    %s/\s\+$//e
endfunction

" }}}
"===[ Autocommands setup ]=== {{{
augroup usr
    autocmd!

    autocmd BufWrite    *.c,*.cpp,*.h,*.hpp,*.py,*.java,*.js,*.php,*.s,*.sh,*.bash,*.rst,*.go,*.f,*.r,*.mat,*.tex,*.json call TrimSpace()
    autocmd BufEnter    *.c,*.cpp,*.md,*.py,*.rst,*.sh,*.vim,*.gitconfig,vimrc silent call EnableFastEsc()
    autocmd BufRead     *.c,*.cpp,*.h,*.hpp,*.py,*.java,*.js,*.php,*.sh syn keyword TODO NOTE Todo Note todo note
    autocmd BufNewfile  *.php r ~/.vim/template/template.php | normal kdd

    autocmd VimEnter    *.c,*.cpp,*.py vsp
augroup END


" }}}

hi link Noise Special

" vim: foldmethod=marker
