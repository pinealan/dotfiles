"===[ Plugin ]=== {{{
filetype off
set nocompatible

call plug#begin('~/.vim/plugged')

Plug 'scrooloose/nerdtree', { 'on': ['NERDTreeToggle', 'NERDTreeTabsToggle'] }
" NERDTree {{{
augroup usr_nerdtree
    autocmd!
    autocmd StdinReadPre * let s:std_in=1
    autocmd BufEnter * if (winnr("$") == 1 && exists("b:NERDTree") && b:NERDTree.isTabTree()) | q | endif
augroup END

let NERDTreeIgnore = [
    \'__pycache__',
    \'_site',
    \'.exe$',
    \'.out$',
    \'coverage',
    \'node_modules',
    \'vendor',
    \'venv',
    \'\.DS_Store',
    \'\.direnv',
    \'\.egg-info',
    \'\.git$',
    \'\.idea',
    \'\.ipynb_checkpoints',
    \'\.mypy_cache',
    \'\.pyc$',
    \'\.pytest_cache',
    \'\.sass-cache',
    \'\.serverless',
    \'\.venv',
    \'\.vscode'
\]
let NERDTreeStatusLine="%{matchstr(getline('.'),'\\s\\zs\\w\\(.*\\)')}"
let NERDTreeShowHidden=1
let NERDTreeSortHiddenFirst=1
" }}}
" Plug 'jistr/vim-nerdtree-tabs', { 'on': 'NERDTreeTabsToggle' }
Plug 'airblade/vim-gitgutter'
Plug 'tpope/vim-fugitive'
Plug 'google/vim-searchindex'
Plug 'godlygeek/tabular'
Plug 'Valloric/YouCompleteMe'
" YouCompleteMe {{{
let g:ycm_min_num_of_chars_for_completion = 2
let g:ycm_max_num_candidates = 25
let g:ycm_filetype_blacklist = {
    \ 'tagbar': 1,
    \ 'qf': 1,
    \ 'notes': 1,
    \ 'markdown': 1,
    \ 'unite': 1,
    \ 'text': 1,
    \ 'vimwiki': 1,
    \ 'pandoc': 1,
    \ 'infolog': 1,
    \ 'mail': 1,
    \ 'html': 1,
    \ 'gitconfig': 1,
    \ 'tex': 1,
    \ 'bib': 0,
    \}
let g:ycm_use_ultisnips_completer = 1

" YCM diagnostic
hi YcmErrorSection cterm=underline ctermfg=196
hi YcmWarningSection cterm=underline ctermfg=196
let g:ycm_error_symbol = 'x'
let g:ycm_warning_symbol = '>'
let g:ycm_open_loclist_on_ycm_diags = 0
let g:ycm_enable_diagnostic_highlighting = 0
let g:ycm_autoclose_preview_window_after_completion = 1

" YCM paths
let g:ycm_global_ycm_extra_conf = "/home/alan/.vim/.ycm_extra_conf.py"
let g:ycm_confirm_extra_conf = 0
let g:ycm_filepath_completion_use_working_dir = 1
let g:ycm_extra_conf_vim_data = ['&filetype']
let g:ycm_python_binary_path = '/usr/bin/python3'
" }}}
Plug 'SirVer/ultisnips'
Plug 'honza/vim-snippets'
Plug 'zirrostig/vim-schlepp'
" Schlepp {{{
let g:Schlepp#allowSquishingLines = 0
let g:Schlepp#allowSquishingBlock = 0
let g:Schlepp#trimWS = 0
" }}}
Plug 'jlanzarotta/bufexplorer'
" BufExplorer {{{
let g:bufExplorerFindActive=0
let g:bufExplorerShowNoName=1
let g:bufExplorerShowRelativePath=1
let g:bufExplorerSortBy='name'
let g:bufExplorerSplitVertSize=51
" }}}
Plug 'jiangmiao/auto-pairs'
Plug 'iamcco/markdown-preview.vim', { 'for': 'markdown' }
Plug '/usr/local/opt/fzf'
Plug 'junegunn/fzf.vim'
Plug 'junegunn/goyo.vim'
" Goyo {{{
let g:goyo_width = 105
" }}}

" Languages
Plug 'Vimjas/vim-python-pep8-indent'
Plug 'dag/vim-fish'
Plug 'othree/html5.vim', { 'for': 'html' }
" HTML5 {{{
"let g:html_exclude_tags = ['html', 'body', 'head']
" }}}
Plug 'pangloss/vim-javascript', { 'for': 'javascript' }
Plug 'leafgarland/typescript-vim', { 'for': 'ts' }
Plug 'mxw/vim-jsx', { 'for': 'javascript' }
Plug 'peitalin/vim-jsx-typescript'
Plug 'mustache/vim-mustache-handlebars'
Plug 'cespare/vim-toml', { 'for': 'toml' }
Plug 'jparise/vim-graphql'
Plug 'lepture/vim-jinja'
Plug 'mechatroner/rainbow_csv'
Plug 'tpope/vim-surround'

call plug#end()
filetype plugin indent on
syntax on
" }}}

"===[ Color scheme ]=== {{{
colorscheme onedark
set background=dark
" }}}

"===[ Default Syntax Package Options ]=== {{{
let g:tex_no_error = 1
" }}}

"===[ Options ]=== {{{
set backspace=eol,start,indent
set cursorline
set nofoldenable
set foldmethod=marker
set hidden
set history=500
set hlsearch
set incsearch
set lazyredraw
set mouse=a
set noshowmatch
set number
set relativenumber
set scrolloff=7
set timeoutlen=220
set whichwrap+=<,>,h,l

set laststatus=2
set statusline=%!MyStatusLine('f')

set showtabline=1
set tabline=%!MyTabLine()

set splitbelow
set splitright

" Tab, indent, wrap
set smarttab
set shiftwidth=4
set softtabstop=4
set tabstop=8

set autoindent
set smartindent
set expandtab
set list
exec "set listchars=tab:\u2015\u2015,trail:\uB7"

set colorcolumn=81
set formatoptions+=t
set textwidth=100
set nowrap

" Regex
set ignorecase
set magic
set smartcase

" Files, backups and undo
set autoread
set fileformat=unix
set fileencoding=utf8
set encoding=utf8
set noswapfile
set nowritebackup

" Command-line completion
set path=.,,**
set wildmenu
set wildignore=__pycache__,*.o,*.pyc,*.git,*.exe
set helpheight=30

" Make
set shellpipe=&>

" }}}

"===[ Mapping ]=== {{{
let mapleader=" "

" Thank me later
noremap ; :
noremap : ;

" Homerow navigation
noremap H 0
noremap J L
noremap K H
noremap L $

noremap $ J
noremap 0 K

" Swap undo
noremap    U   <c-r>
noremap <c-r>     U

" Windows navigation
noremap [p          <c-w>P
noremap [h          <c-w>h
noremap [j          <c-w>j
noremap [k          <c-w>k
noremap [l          <c-w>l
noremap [o          <c-w>o
noremap {<s-h>      <c-w>9<
noremap {<s-j>      <c-w>9-
noremap {<s-k>      <c-w>9+
noremap {<s-l>      <c-w>9>

" Buffer/Error list shortcuts
noremap <c-q>      :qa<cr>
noremap <c-s>      :update<cr>
noremap <c-r>      :edit<cr>
noremap <c-h>      :tabprev<cr>
noremap <c-l>      :tabnext<cr>
noremap <c-j>      :bnext<cr>
noremap <c-k>      :bprev<cr>
noremap <c-n>      :cnext<cr>
noremap <c-p>      :cprev<cr>

" Normal mode command shortchuts
nnoremap <cr>               o<esc>
nnoremap <F9>               :echo HighlightName()<cr>
nnoremap <leader>c          :echo HighlightName()<cr>
nnoremap <leader>w          :w !sudo tee % > /dev/null
nnoremap <leader>f          :set filetype=

" create a fold for the pair of parenthesis/brackets under the cursor
nnoremap <leader>o          v%zF

nnoremap <silent> -             :call Underline('-')<cr>
nnoremap <silent> =             :call Underline('=')<cr>
nnoremap <silent> <leader>'     :vsp<cr>
nnoremap <silent> <leader>e     :vsplit $MYVIMRC<cr>
nnoremap <silent> <leader>m     :silent! make \| redraw! \| call ReportBuildResult()<cr>
nnoremap <silent> <leader>n     :vnew \| set ft=markdown<cr>
nnoremap <silent> <leader>p     :setlocal paste!<cr>
nnoremap <silent> <leader>q     :q<cr>

" toggles
nnoremap <leader>s          :setlocal spell!<cr>
nnoremap <leader><tab>      :setlocal wrap!<cr>
nnoremap <leader><leader>   :setlocal hlsearch!<cr>
nnoremap <leader><esc>      :call ToggleFastEsc()<cr>
nnoremap <leader>1          :setl statusline=%!MyStatusLine('t')<cr>
nnoremap <leader>2          :setl statusline=%!MyStatusLine('f')<cr>

" Plugin mapping
nnoremap <c-e>          :NERDTreeToggle<cr>

nnoremap <leader>t      :Tabularize /

nnoremap <silent> <F8>  :MarkdownPreview<cr>

nnoremap <leader>x      :YcmCompleter FixIt<cr>
nnoremap <leader>g      :YcmCompleter GoToDefinition<cr>
nnoremap <leader>r      :YcmCompleter GoToReferences<cr>

nnoremap bhs      :BufExplorerHorizontalSplit<cr>
nnoremap bvs      :BufExplorerVerticalSplit<cr>

let g:UltiSnipsExpandTrigger="<c-e>"
let g:UltiSnipsJumpForwardTrigger="<c-j>"
let g:UltiSnipsJumpBackwardTrigger="<c-k>"

vmap <unique> <up>      <Plug>SchleppUp
vmap <unique> <down>    <Plug>SchleppDown
vmap <unique> <left>    <Plug>SchleppLeft
vmap <unique> <right>   <Plug>SchleppRight
" }}}

"===[ Commands ]=== {{{
command Bhs BufExplorerHorizontalSplit
command Bvs BufExplorerVerticalSplit
" }}}

"===[ Abbrevation ]=== {{{
" Common typos
iabbrev fro         for
iabbrev adn         and
iabbrev swithc      switch
iabbrev swihtc      switch
iabbrev siwthc      switch
iabbrev csae        case
iabbrev caes        case

" Auto closing brackets
"inoremap " ""<left>
"inoremap ' ''<left>
"inoremap ( ()<left>
"inoremap [ []<left>
"inoremap { {}<left>
"inoremap {<CR> {<CR>}<ESC>O
"inoremap {;<CR> {<CR>};<ESC>O
" }}}

"===[ Helper functions]=== {{{
function! HasPaste()
    if &paste
        return 'PASTE MODE'
    endif
    return ''
endfunction

" Format home directory into ~ and truncate the beginning of the path.
function! ShortCwd(len)
    let cwd=getcwd()
    let cwd=substitute(cwd, '/home/[^/]\+', '~', '')
    let cwd=substitute(cwd, '[^~].*\(.\{' . a:len . '\}$\)\@=', '/...', '')
    let cwd=substitute(cwd, '\.\.\.[^/]*', '...', '')
    return cwd
endfunction

function! PathFilename(path)
    let name=split(a:path, '/')
    if len(name) == 0
        return '[no name]'
    elseif len(name) < 4
        return a:path
    else
        return name[0] . '/.../' . name[-2] . '/' . name[-1]
endfunction

function! MyTabLabel(n)
    let wincount = tabpagewinnr(a:n, '$')
    let winnr = tabpagewinnr(a:n)
    let bufls = tabpagebuflist(a:n)
    return PathFilename(bufname(bufls[winnr - 1])) . ' (' . wincount . ')'
endfunction

function! MyTabLine()
    let s = ''
    for i in range(tabpagenr('$'))
        if i + 1 == tabpagenr()
            let s .= '%#TabLineSel#'
        else
            let s .= '%#TabLine#'
        endif
        let s .= '%' . (i + 1) . 'T'
        let s .= ' %{MyTabLabel(' . (i + 1) . ')} '
    endfor

    let s .= '%#TabLineFill#%T'

    if tabpagenr('$') > 1
        let s .= '%=%#TabLine#%999Xclose'
    endif
    return s
endfunction

function! HighlightName()
    let id  = synID(line("."), col("."), 1)
    let tid = synID(line("."), col("."), 0)

    let fg  = synIDattr(synIDtrans(id), "fg")
    let bg  = synIDattr(synIDtrans(id), "bg")
    let hi  = synIDattr(id, "name")
    let lo  = synIDattr(synIDtrans(id), "name")
    let tr  = synIDattr(tid, "name")

    return "hi: ".hi." \| lo: ".lo." \| trans: ".tr." \| fg: ".fg." \| bg: ".bg
endfunction

function! EnableFastEsc()
    let b:fastesc=1
    inoremap <buffer> jj <esc>
    echom "Enabled insert mdoe fast <esc>"
endfunction!

function! DisableFastEsc()
    unlet b:fastesc
    iunmap <buffer> jj
    echom "Disabled insert mdoe fast <esc>"
endfunction!

function! ToggleFastEsc()
    if get(b:, 'fastesc', 0)
        call DisableFastEsc()
    else
        call EnableFastEsc()
    endif
endfunction

function! Underline(symbol)
    let line = getline('.')
    let llen = strlen(line)
    let str = ''
    if llen > 0
        let widx = match(line, '\S')
        for ii in range(widx)
            let str = str . " "
        endfor
        for ii in range(llen - widx)
            let str = str . a:symbol
        endfor
        call setreg("y", str)
        exec "normal! o\<esc>\"yp"
    endif
endfunction

function! ReportBuildResult()
    if len(getqflist()) != 0
        copen | cc
        echohl Tag
        echo "Build fail!"
        echohl None
    else
        echo "Build success!"
    endif
endfunction

function! GitBranch()
    let l:branchname = system("git rev-parse --abbrev-ref HEAD 2>/dev/null | tr -d '\n'")
    return strlen(l:branchname) > 0 ? '  ' . l:branchname . ' ' : ''
endfunction

function! MyStatusLine(path)
    let gitbr = "%#StatusLineGit#%{GitBranch()}%#User{1}#\ "
    let flags = "%{HasPaste()}%r%m"
    let index = "%l,\%-2c\ \|%3p%%"
    return gitbr . flags . "%" . a:path . "%=%y\ " . index
endfunction

function! TrimSpace()
    %s/\s\+$//e
endfunction

function! TryTrimSpace()
    if &ft =~ 'markdown'
        return
    else
        call TrimSpace()
    endif
endfunction
" }}}

"===[ Autocommands setup ]=== {{{
augroup usr
    autocmd!

    autocmd BufWrite    * call TryTrimSpace()
    autocmd BufEnter    * silent call EnableFastEsc()
    autocmd BufRead     *.c,*.cpp,*.h,*.hpp,*.py,*.java,*.js,*.php,*.sh syn keyword TODO NOTE Todo Note todo note
    autocmd BufNewfile  *.php r ~/.vim/template/template.php | normal kdd

    "autocmd VimEnter    *.c,*.cpp,*.py vsp
augroup END
" }}}

hi link Noise Special

" vim: foldmethod=marker
